buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.34.0'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'idea'

def projectName = 'juniper'
group 'com.github.tommyettinger'
version "$VERSION_NAME"

import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
}

plainJavadocJar.archiveBaseName.set(project.name)

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// https://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc).tap {
            configureEach {
                // The -quiet is because of some sort of weird JDK JavaCompiler bug:
                // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
                options.addStringOption('Xdoclint:none,-missing', '-quiet')
            }
        }
    }
}

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

idea.module.excludeDirs += [file("src/main/java/com/github/tommyettinger/emu/")]
[compileJava, compileTestJava, javadoc]*.exclude("com/github/tommyettinger/emu")
idea.module.excludeDirs += [file("docs/")]

// JavaDocs will be published inside the docs/ folder, which you can easily put on GitHub Pages in your repo settings.
javadoc.destinationDir = file('docs/apidocs')

jar {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    google()
    maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
//    maven { url 'https://jitpack.io' }
}

dependencies {
    api "com.github.tommyettinger:digital:$digitalVersion"
    testImplementation "com.github.tommyettinger:jdkgdxds:$jdkgdxdsVersion"
    testImplementation "org.apache.fory:fory-core:$foryVersion"
    //noinspection GDXOutdatedVersionGradleGroovy
    testImplementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testImplementation "junit:junit:4.13.2"
}
